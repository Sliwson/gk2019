R""(
	#version 460 core

	struct Material 
	{
		sampler2D diffuse;
		sampler2D specular;
		float shininess;
	};

	struct PointLight
	{
		vec3 position;
		vec3 color;

		vec3 ambient;
		vec3 diffuse;
		vec3 specular;

		float constant;
		float linear;
		float quadratic;
	};

	struct DirectionalLight
	{
		vec3 position;
		vec3 direction;
		float cutOff;
		vec3 color;

		vec3 ambient;
		vec3 diffuse;
		vec3 specular;

		float constant;
		float linear;
		float quadratic;
	};

	out vec4 fragmentColor;

	in vec3 fragmentPosition;
	in vec3 normalVector;
	in vec2 textureCoordinates;

	uniform vec3 viewPosition;
	uniform Material material;
	uniform PointLight pointLight;

	vec4 GetPointLightColor()
	{
		float distance = length(pointLight.position - fragmentPosition);
		float attenuation = 1.0 / (pointLight.constant + pointLight.linear * distance + pointLight.quadratic * distance * distance);

		vec3 diffuseColor = vec3(texture(material.diffuse, textureCoordinates));
		vec3 specularColor = vec3(texture(material.specular, textureCoordinates));

		vec3 normal = normalize(normalVector);
		vec3 pointLightVector = normalize(pointLight.position - fragmentPosition);  
		float diffuseWeight = max(dot(normal, pointLightVector), 0.0);

		vec3 viewDirection = normalize(viewPosition - fragmentPosition);
		vec3 reflectDirection = reflect(-pointLightVector, normal);
		float specularWeight = pow(max(dot(viewDirection, reflectDirection), 0.0), material.shininess);

		vec3 ambient = pointLight.ambient * diffuseColor * pointLight.color;
		vec3 diffuse = pointLight.diffuse * diffuseColor * diffuseWeight * pointLight.color;
		vec3 specular = pointLight.specular * specularColor * specularWeight * pointLight.color;

		return vec4((ambient + diffuse + specular) * attenuation, 1.0);
	}

	void main()
	{
		fragmentColor = GetPointLightColor();
	} 
)""